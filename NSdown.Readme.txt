NSdown::Transfer	[/NOUNLOAD]
					[/METHOD GET|POST]
					/URL url
					[/LOCAL file|MEMORY|NONE]
					[/HEADERS hdr]
					[/DATA data | /DATAFILE file]
					[/TIMEOUTCONNECT msec] [/TIMEOUTRECONNECT msec]
					[/OPTCONNECTRETRIES count] [/OPTCONNECTTIMEOUT msec] [/OPTRECEIVETIMEOUT msec]
					[/PROXY server] [/PROXYUSER user] [/PROXYPASS pass]
					[/REFERER url]
					/END

Parameters:
	/NOUNLOAD					| Must be specified, otherwise NSIS will unload NSdown at runtime
	/METHOD GET|POST			| HTTP Method. Default is GET
	/URL url					| HTTP or HTTPS
	/LOCAL file|MEMORY|NONE		| The remote content can be downloaded to either a local file, to memory, or not downloaded at all. Default is NONE
	/HEADERS hdr				| Additional HTTP headers, separated by CRLF (\r\n)
	/DATA data					| Additional data to be sent as part of the HTTP request
	/DATAFILE file				| Additional data to be sent as part of the HTTP request (read from the specified file)
	/TIMEOUTCONNECT msec		| Keep trying to connect for "msec" milliseconds. Default is 0 (only one attempt to connect is made)
	/TIMEOUTRECONNECT msec		| Keep trying to reconnect for "msec" milliseconds, if the connection drops while downloading. Default is 0 (no attempts to reconnect are made)
	/OPTCONNECTRETRIES count	| InternetSetOption( INTERNET_OPTION_CONNECT_RETRIES ). Relevant only for remote hosts with multiple IPs!
	/OPTCONNECTTIMEOUT msec		| InternetSetOption( INTERNET_OPTION_CONNECT_TIMEOUT )
	/OPTRECEIVETIMEOUT msec		| InternetSetOption( INTERNET_OPTION_RECEIVE_TIMEOUT )
	/PROXY server				| CERN type proxies (ex: "http=http://my_proxy:my_port"). If Internet Explorer is installed, SOCKS proxies are supported too
	/PROXYUSER user				| Optional username for authenticated proxy servers
	/PROXYPASS pass				| Optional password for authenticated proxy servers
	/REFERER url				| Optional referer URL, passed to InternetOpenRequest
	/INTERNETFLAGS flags		| Combination of INTERNET_FLAG_XXX passed to InternetOpenRequest. Default is 0x84082200 (INTERNET_FLAG_NO_CACHE_WRITE|INTERNET_FLAG_IGNORE_CERT_DATE_INVALID|INTERNET_FLAG_NO_COOKIES|INTERNET_FLAG_NO_UI|INTERNET_FLAG_RELOAD)
	/SECURITYFLAGS flags		| Combination of SECURITY_FLAG_XXX passed to InternetSetOption(INTERNET_OPTION_SECURITY_FLAGS). Default is 0x2080 (SECURITY_FLAG_IGNORE_REVOCATION|SECURITY_FLAG_IGNORE_CERT_DATE_INVALID)
	/END						| Must conclude the parameter list, otherwise the NSIS stack will be emptied...

Return:
	[Stack] An unique transfer ID. Useful to query the transfer status later

Examples:

	; Download to local file
	NSdown::Transfer /NOUNLOAD /URL "http://live.sysinternals.com/Files/SysinternalsSuite.zip" /LOCAL "$EXEDIR\SysinternalsSuite.zip" /TIMEOUTCONNECT 60000 /TIMEOUTRECONNECT 300000 /END
	Pop $0	; Transfer ID

	; Download to memory
	NSdown::Transfer /NOUNLOAD /URL "http://live.sysinternals.com/Files/SysinternalsSuite.zip" /LOCAL MEMORY /END
	Pop $0	; Transfer ID

	; Send the HTTP request but don't download the content
	NSdown::Transfer /NOUNLOAD /URL "http://mydomain.com:800?param1=va1&param2=val2" /LOCAL NONE /END
	Pop $0	; Transfer ID

	; POST a form
	NSdown::Transfer /NOUNLOAD /METHOD POST /URL "http://httpbin.org/post" /LOCAL "Post.txt" /HEADERS "Content-Type: application/x-www-form-urlencoded" /DATA "user=MY+NAME&pass=MY+PASSWORD" /END
	Pop R0	; Transfer ID
_________________________________________________________________________________________________

NSdown::Enumerate [/NOUNLOAD] all|downloading|waiting|completed

Return:
	[Stack] Item count (N)
	[Stack] ID1
	[Stack] ID2
	[Stack] ...
	[Stack] IDn

Example:
	NSdown::Enumerate /NOUNLOAD all
	Pop $1 ; Count
	${For} $0 1 $1
		Pop $2
		DetailPrint "[$0] Transfer ID = $2"
	${Next}
_________________________________________________________________________________________________

NSdown::Query	[/NOUNLOAD]
				TransferID
				[/STATUS] [/WININETSTATUS]
				[/METHOD] [/URL] [/IP] [/PROXY] [/LOCAL]
				[/SENTHEADERS] [/RECVHEADERS]
				[/FILESIZE] [/RECVSIZE] [/PERCENT] [/SPEEDBYTES] [/SPEED]
				[/TIMEWAITING] [/TIMEDOWNLOADING]
				[/ERRORCODE] [/ERRORTEXT]
				/END

				The TransferID is mandatory. The list must be ended with /END


Parameters:
	/NOUNLOAD				| Must be specified, otherwise NSIS will unload NSdown at runtime
	TransferID				| The transfer ID returned by NSdown::Transfer
	/STATUS					| Transfer status (downloading|waiting|completed)
	/WININETSTATUS			| The last status reported by the InternetCallback (http://msdn.microsoft.com/EN-US/library/windows/desktop/aa385121(v=vs.85).aspx)
	/METHOD					| The HTTP method in use (GET, POST, HEAD, etc.)
	/URL					| Full URL
	/IP						| Server's IP address, or an empty string
	/PROXY					| The proxy server, or an empty string
	/LOCAL					| The local destination (file|MEMORY|NONE)
	/SENTHEADERS			| The raw HTTP headers sent to the server
	/RECVHEADERS			| The raw HTTP headers received from the server
	/FILESIZE				| The remote content length (can exceed 4GB (32-bit boundary))
	/RECVSIZE				| The amount of bytes received so far
	/PERCENT				| Percent (0-100)
	/SPEEDBYTES				| Transfer speed (bytes/second)
	/SPEED					| Transfer speed (nicely formatted, ex: "1,4 MB/s")
	/TIMEWAITING			| Amount of time (milliseconds) spent waiting in queue
	/TIMEDOWNLOADING		| Amount of time (milliseconds) spent transferring
	/ERRORCODE				| Win32 error code, or HTTP status code
	/ERRORTEXT				| The error explained
	/END					| Must conclude the parameter list, otherwise the NSIS stack will be emptied...

Return:
	A value for each parameter, in the same order
	[Stack] Value1
	[Stack] Value2
	[Stack] ...
	[Stack] ValueN

Example:
	NSdown::Query /NOUNLOAD $ID /STATUS /URL /PERCENT /SPEED /END
	Pop $0	;Status
	Pop $1	;URL
	Pop $2	;Percent
	Pop $3	;Speed
	DetailPrint "Status:$0, URL:$1, Percent:$2, Speed:$3"
_________________________________________________________________________________________________

NSdown::QueryGlobal [/NOUNLOAD]
					[/COUNTTOTAL] [/COUNTWAITING] [/COUNTDOWNLOADING] [/COUNTCOMPLETED]
					[/SPEEDBYTES] [/SPEED]
					[/COUNTTHREADS]
					/END

Parameters:
	/NOUNLOAD				| Must be specified, otherwise NSIS will unload NSdown at runtime
	/COUNTTOTAL				| The overall number of transfers (waiting + downloading + completed)
	/COUNTWAITING			| The number of waiting transfers
	/COUNTDOWNLOADING		| The number of transfers in progress
	/COUNTCOMPLETED			| The number of completed transfers
	/SPEEDBYTES				| The combined speed of transfers in progress (bytes/second)
	/SPEED					| The combined speed of transfers in progress (nicely formatted, ex: "1,4 MB/s")
	/COUNTTHREADS			| Worker thread count
	/END					| Must conclude the parameter list, otherwise the NSIS stack will be emptied...

Return:
	A value for each parameter, in the same order
	[Stack] Value1
	[Stack] Value2
	[Stack] ...
	[Stack] ValueN

Example:
	NSdown::QueryGlobal /NOUNLOAD /COUNTTOTAL /COUNTCOMPLETED /COUNTDOWNLOADING /SPEED /COUNTTHREADS /END
	Pop $R0 ; Total
	Pop $R1 ; Completed
	Pop $R2 ; Downloading
	Pop $R3 ; Speed
	Pop $R4 ; Worker threads

	DetailPrint "Transferring $R1+$R2/$R0 items at $R3 using $R4 worker threads"
_________________________________________________________________________________________________

